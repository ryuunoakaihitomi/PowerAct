// gradle task: bintrayUpload

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = PROJ_GROUP
version = PROJ_VERSION
project.archivesBaseName = PROJ_ARTIFACT_ID


task javadoc(type: Javadoc) {
    delete(destinationDir)
    options {
        locale 'en_US'
        encoding 'UTF-8'
        charSet 'UTF-8'
        author true
        version true
        title "$PROJ_NAME $PROJ_VERSION"
        memberLevel = JavadocMemberLevel.PUBLIC
    }
    options.showFromPublic()

    // https://github.com/gradle/gradle/issues/2354#issuecomment-364857181
    options.addBooleanOption('notree', true)
    options.addBooleanOption('noindex', true)
    options.addBooleanOption('nohelp', true)
    options.addBooleanOption('nonavbar', true)

    source = android.sourceSets.main.java.sourceFiles
    // https://stackoverflow.com/a/49172753
    configurations.implementation.setCanBeResolved(true)
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator)) + configurations.implementation

    exclude '**/internal/**'

    // cannot find symbol variable BuildConfig
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.getJavaCompileProvider().get().classpath.files
    })

    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = PROJ_GROUP
                artifactId = PROJ_ARTIFACT_ID
                version = PROJ_VERSION

                artifact javadocJar
                artifact sourcesJar

                pom {
                    description = PROJ_DESCRIPTION
                    name = PROJ_NAME
                    url = PROJ_WEBSITE_URL

                    licenses {
                        // ['Apache-2.0']
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = DEVELOPER_ID
                            name = DEVELOPER_NAME
                            email = DEVELOPER_EMAIL
                        }
                    }
                    scm {
                        connection = PROJ_VCS_URL
                        url = PROJ_WEBSITE_URL
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode()['dependencies'][0] ?: asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

bintray {

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty('BINTRAY_USER')
    key = properties.getProperty('BINTRAY_KEY')
    publications = ['release']

    dryRun = false
    publish = true
//    override = true

    pkg {
        repo = 'maven'
        name = PROJ_NAME
        licenses = ['Apache-2.0']
        vcsUrl = PROJ_VCS_URL
        websiteUrl = PROJ_WEBSITE_URL
        issueTrackerUrl = PROJ_ISSUE_TRACKER_URL
        publicDownloadNumbers = true
        version {
            name = PROJ_VERSION
            desc = PROJ_DESCRIPTION
            vcsTag = PROJ_VERSION
            released = new Date()

            gpg {
                sign = true // *.asc
            }
        }
    }
}
